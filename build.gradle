buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
    }
    dependencies {
        classpath group: 'com.jfrog.bintray.gradle', name: 'gradle-bintray-plugin', version: '1.7.3'
        classpath group: 'com.netflix.nebula', name: 'nebula-dependency-recommender', version: '4.2.0'
        classpath group: 'com.netflix.nebula', name: 'nebula-publishing-plugin', version: '4.2.0'
        classpath group: 'org.starchartlabs.flare', name: 'flare-operations-plugins', version: '0.2.0'
        classpath group: 'org.starchartlabs.flare', name: 'flare-publishing-plugins', version: '0.2.0'
    }
}

plugins {
    id 'org.sonarqube' version '2.5'
}

if (project.hasProperty('sonarLogin')) {
    System.setProperty('sonar.login', "${sonarLogin}")
}

sonarqube {
    properties {
        property 'sonar.projectName', 'Calamari'
        property 'sonar.projectKey', 'org.starchartlabs.calamari:calamari'
        property 'sonar.organization', 'starchart-labs'
        property 'sonar.junit.reportsPath', file("${buildDir}/test-results/test")
        property 'sonar.core.codeCoveragePlugin', 'jacoco'
        property 'sonar.jacoco.reportPaths', "${buildDir}/jacoco/test.exec"
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}

apply plugin: 'org.starchartlabs.flare.merge-coverage-reports'

allprojects{
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'jacoco'
    apply plugin: 'nebula.dependency-recommender'
    apply plugin: 'nebula.maven-resolved-dependencies'
    apply plugin: 'org.starchartlabs.flare.dependency-insight'

    dependencyRecommendations {
        propertiesFile file: file("${rootDir}/dependencies.properties")
    }

    group = 'org.starchartlabs.calamari'

    sourceCompatibility = "${javaVersion}"

    //Always download sources, to allow debugging, and use Eclipse containers for greater portability
    eclipse {
        classpath {
            downloadSources=true
            containers = [ "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-${sourceCompatibility}".toString() ]
        }
    }

    repositories {
        mavenCentral()
    }

}

subprojects{
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'nebula.maven-base-publish'
    apply plugin: 'nebula.maven-dependencies'
    apply plugin: 'org.starchartlabs.flare.increase-test-logging'
    apply plugin: 'org.starchartlabs.flare.source-jars'
    apply plugin: 'org.starchartlabs.flare.published-project'
    
    //Setup default test behavior, including failure logging
    test {
        useTestNG() {
            useDefaultListeners = true
        }
    }
    
    sonarqube {
        properties {
            property 'sonar.projectKey', "${project.group}:${project.name}"
            property 'sonar.junit.reportsPath', file("${buildDir}/test-results/test")
            property 'sonar.core.codeCoveragePlugin', 'jacoco'
            property 'sonar.jacoco.reportPaths', "${buildDir}/jacoco/test.exec"
            properties['sonar.sources'] = sourceSets.main.java.srcDirs.findAll { it.exists() && !it.path.contains('resources') } ?: []
        }
    }
    
    //Force transitive dependency versions
    //jackson - pulled in by jjwt, was 2.9.6, vulnerability update to 2.9.8
    configurations.all {
        resolutionStrategy {
            force 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
        }
    }

    //Apply module naming to all projects
    //Add LICENSE so it is included in all JARs, as well as dependent licenses, fulfilling the "distributions include license" requirement
    jar {
        manifest {
            attributes("Automatic-Module-Name": "${project.group}.${project.name  - 'calamari-'}".replaceAll("-", "."))
        }
        
        from("${rootDir}"){
            include 'LICENSE'
        }
    }
    
    //All projects should provide source code and javadoc, and upload these with any released artifacts
    sourcesJar {
        from("${rootDir}"){
            include 'LICENSE'
        }
    }

    javadocJar{
        from("${rootDir}"){
            include 'LICENSE'
        }
    }

    //If not a remote run, publish to local only
    if(!project.hasProperty('remoteDeploy')){
        publishing {
            repositories {
                mavenLocal()
            }
        }
    }
    
    publishedInfo{
        url = 'https://github.com/StarChart-Labs/calamari'
      
        scm{
            github 'StarChart-Labs', 'calamari'
        }
      
        licenses{
            mit('repo')
        }
      
        developers{
            github 'romeara', 'Ryan OMeara'
        }
    }
    
    bintray {
        user = System.getProperty('bintray_user')
        key = System.getProperty('bintray_key')
        publications = [ 'nebula' ]

        if(!project.hasProperty('remoteDeploy')){
            dryRun = true
        }

        pkg {
            repo = 'calamari'
            name = "${project.name}"
            userOrg = 'starchart-labs'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/StarChart-Labs/calamari'

            version {
                name = "${project.version}"
                desc = "${project.name} release ${project.version} final"
                released  = new Date()
                vcsTag = "${project.version}"

                gpg {
                    sign = true
                }
            }
        }
    }
    
}
